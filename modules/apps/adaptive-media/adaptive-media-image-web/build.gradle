import com.liferay.gradle.util.copy.StripPathSegmentsAction

configurations {
	picturefill
}

task buildPictureFill(type: Copy)

String pictureFillVersion = "3.0.2"

buildPictureFill {
	eachFile new StripPathSegmentsAction(6)

	from {
		zipTree(configurations.picturefill.singleFile)
	}

	include "META-INF/resources/webjars/picturefill/${pictureFillVersion}/dist/picturefill.min.js"
	includeEmptyDirs = false
	into "classes/META-INF/resources"
}

classes {
	dependsOn buildPictureFill
}

dependencies {
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations"
	compileOnly group: "org.osgi", name: "osgi.core"
	compileOnly project(":apps:adaptive-media:adaptive-media-api")
	compileOnly project(":apps:adaptive-media:adaptive-media-image-api")
	compileOnly project(":core:petra:petra-string")

	picturefill group: "org.webjars.bower", name: "picturefill", transitive: false, version: pictureFillVersion

	testCompile project(":core:petra:petra-lang")
}