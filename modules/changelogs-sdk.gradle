import org.gradle.util.VersionNumber

buildscript {
	apply from: file("build-buildscript.gradle"), to: buildscript

	repositories {
		maven {
			url "https://repository-cdn.liferay.com/nexus/content/groups/public"
		}
	}
}

File changelogPropertiesFile = file("sdk/" + projectName + "/CHANGELOG.properties")

task buildChangelogs
task recordCommits

buildChangelogs {
	dependsOn recordCommits

	description = "Builds the changelogs for each published version in `CHANGELOG.markdown`."
	group = "changelogs"

	doLast {
		assert project.properties["projectName"] : "Please provide a value for the '-PprojectName' property"

		changelogPropertiesFile.getText("UTF-8").eachLine {
			String recordCommitLine ->

			ByteArrayOutputStream standardOutputByteArrayOutputStream = new ByteArrayOutputStream()

			exec {
				commandLine "git", "--no-pager", "log", "--follow", "--pretty=format:%H:%cd:%s", "-p", "6a1ce18c602e..ed274081137c5", "--", "modules/sdk/gradle-plugins/build.gradle"
				standardOutput = standardOutputByteArrayOutputStream
				workingDir = projectDir.parentFile
			}

			String gitLogOutput = standardOutputByteArrayOutputStream.toString()

			String commitId = ""
			String ticket = ""
			String version = "4.4.101"

			gitLogOutput.eachLine {
				String gitLogLine ->

				def matcher = gitLogLine =~ /([a-f0-9]+):.+:([A-Z]+-\d+).*/

				if (matcher.find()) {
					commitId = matcher.group(1)
					ticket = matcher.group(2)
				}

				matcher = gitLogLine =~ /\+\s*compile group: .*, name: \"(.+)", version: \"(\d+\.\d+\.\d+)\"/

				if (matcher.find()) {
					String dependencyName = matcher.group(1)
					String dependencyVersion = matcher.group(2)

					String changelog = """\
						|## ${version} - 1970-01-01
						|
						|### Changed
						|- [${ticket}]: Update the [${dependencyName}] dependency to version ${dependencyVersion}.""".stripMargin()

					logger.lifecycle changelog
				}
			}
		}

		logger.lifecycle "Updated CHANGELOG.markdown for '{}'", projectName
	}
}

recordCommits {
	description = "Records the commit ID for each published version in `CHANGELOG.properties`."
	group = "changelogs"

	doLast {
		assert project.properties["projectName"] : "Please provide a value for the '-PprojectName' property"

		ByteArrayOutputStream standardOutputByteArrayOutputStream = new ByteArrayOutputStream()

		exec {
			commandLine "git", "--no-pager", "log", "--follow", "--format=fuller", "-p", "--", "modules/sdk/" + projectName + "/"
			standardOutput = standardOutputByteArrayOutputStream
			workingDir = projectDir.parentFile
		}

		String gitLogOutput = standardOutputByteArrayOutputStream.toString()

		List<String> commits = gitLogOutput.split("\ncommit ")

		VersionNumber lastVersionNumber = null
		Map<VersionNumber, String> recordCommitIDs = [:]

		for (int i = 0; i < (commits.size() - 1); i++) {
			String commit = commits.get(i)
			String previousCommit = commits.get(i + 1)

			String diff = commit.split("\n", 2)[1]

			int x = diff.indexOf("/" + projectName + "/bnd.bnd");

			int y = diff.indexOf("-Bundle-Version: ", x)

			if ((x != -1) && (y != -1)) {
				String publishedVersion = diff.substring(y + 17, diff.indexOf("\n", y))

				VersionNumber publishedVersionNumber = VersionNumber.parse(publishedVersion)

				if (!recordCommitIDs.containsKey(publishedVersionNumber) && (publishedVersionNumber.major > 0) && ((lastVersionNumber == null) || (publishedVersionNumber.compareTo(lastVersionNumber) < 0))) {
					recordCommitIDs.put(publishedVersionNumber, previousCommit.split("\n", 2)[0])

					lastVersionNumber = publishedVersionNumber
				}
			}
		}

		changelogPropertiesFile.text = recordCommitIDs.collect {
			VersionNumber versionNumber, String commitId ->

			"git-id[${versionNumber}]=${commitId}"
		} join "\n"

		logger.lifecycle "Updated '{}'", changelogPropertiesFile
	}
}