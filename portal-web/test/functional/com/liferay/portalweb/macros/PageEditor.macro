definition {
	macro addFragment {
		PageEditor.expandFragmentCollection(collectionName = "${collectionName}");

		Click.clickNoMouseOverNoWaitForVisible(
			key_fragmentName = "${fragmentName}",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_ADD_FRAGMENT_BUTTON");

		AssertElementPresent(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER");
	}

	macro addWidget {
		PageEditor.gotoTab(tabName = "Widgets");

		Type.sendKeysApplicationSearch(
			locator1 = "PageEditor#WIDGETS_APPLICATION_SEARCH_FIELD",
			value1 = "${portletName}");

		var key_portletName = "${portletName}";

		DragAndDrop(
			locator1 = "PageEditor#WIDGETS_ADD_PORTLET_DRAG_LINK",
			locator2 = "PageEditor#FRAGMENT_DROP_TARGET",
			value1 = "${portletName}");

		Pause(locator1 = "5000");

		AssertTextEquals.assertPartialText(
			locator1 = "Portlet#PORTLET_CONTENT",
			value1 = "${portletName}");
	}

	macro clickBack {
		Click(locator1 = "Icon#ANGLE_LEFT_BACK");
	}

	macro clickDiscardDraft {
		Button.clickNoError(button = "Discard Draft");

		Confirm();
	}

	macro clickPublish {
		Button.click(button = "Publish");
	}

	macro editFragmentImage {
		Click(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD");

		Click(locator1 = "PageEditor#EDITABLE_FIELD_TOOLBAR_IMAGE_PROPERTIES_BUTTON");

		Button.clickSelect();

		SelectFrame(locator1 = "IFrame#DIALOG");

		AssertElementPresent(locator1 = "Button#SELECT_FILE");

		UploadCommonFile.uploadCommonFileHiddenNoMouseOver(
			locator1 = "TextInput#FILE",
			value1 = "${fileName}");

		Pause(locator1 = "3000");

		SelectFrameTop();

		Click(locator1 = "Button#ADD");
	}

	macro editFragmentText {
		Click(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD");

		Click(locator1 = "PageEditor#EDITABLE_FIELD_TOOLBAR_EDIT_BUTTON");

		Type(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD_CK_EDITOR",
			value1 = "${text}");

		Pause(locator1 = "3000");
	}

	macro expandFragmentCollection {
		AssertVisible(
			key_collectionName = "${collectionName}",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_COLLECTION_PANEL");

		if (IsElementPresent(locator1 = "PageEditor#FRAGMENT_SIDEBAR_COLLECTION_PANEL_COLLAPSED", key_collectionName = "${collectionName}")) {
			AssertClick(
				key_collectionName = "${collectionName}",
				locator1 = "PageEditor#FRAGMENT_SIDEBAR_COLLECTION_PANEL_COLLAPSED",
				value1 = "${collectionName}");
		}

		AssertElementPresent(
			key_collectionName = "${collectionName}",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_COLLECTION_PANEL_EXPANDED");
	}

	macro gotoTab {
		Click(
			key_tabName = "${tabName}",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_TAB_BUTTON");
	}

	macro removeFragment {
		MouseOver.mouseOverNotVisible(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER");

		Click(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER_DELETE_BUTTON");

		AssertElementNotPresent(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER");
	}

	macro removeSection {
		MouseOver.mouseOverNotVisible(
			key_position = "${position}",
			locator1 = "PageEditor#SECTION");

		Click(
			key_position = "${position}",
			locator1 = "PageEditor#SECTION_HEADER_DELETE_BUTTON");
	}

	macro viewAutoSaveMessage {
		AssertTextEquals.assertPartialText(
			locator1 = "PageEditor#AUTOSAVE_MESSAGE",
			value1 = "Draft saved at");
	}

	macro viewCollectionNotPresent {
		AssertElementNotPresent(
			key_collectionName = "${collectionName}",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_COLLECTION_PANEL");
	}

	macro viewEditorEmpty {
		AssertElementPresent(locator1 = "PageEditor#EMPTY_MESSAGE");
	}

	macro viewFragment {
		AssertElementPresent(
			key_fragmentName = "${fragmentName}",
			locator1 = "Fragment#FRAGMENT_HEADER");
	}

	macro viewFragmentEditableText {
		AssertTextEquals(
			key_fragmentName = "${fragmentName}",
			key_id = "${id}",
			locator1 = "Fragment#EDITABLE_FIELD",
			value1 = "${editableText}");
	}

	macro viewFragmentOrder {
		AssertElementPresent(
			key_fragmentName = "${fragmentName}",
			key_position = "${position}",
			locator1 = "Fragment#FRAGMENT_HEADER_POSITION");
	}
}